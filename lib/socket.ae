// Public facing API

struct Socket {
    fd: i32
}

enum SocketMode {
    UDP
    TCP
}

def Socket::connect(host: string, port: i32, mode: SocketMode): Socket {
    let sock: Socket

	// Creating socket
	sock.fd = match mode {
        UDP => _c_socket(AF_INET, SOCK_DGRAM, IPPROTO_UDP),
        TCP => _c_socket(AF_INET, SOCK_STREAM, IPPROTO_TCP)
    }
	if (sock.fd < 0){
        println(`Error creating socket: {strerror(errno)}`)
        exit(1)
	}

	// Adding server address info. to struct
	let server_address: SockAddrIn
	server_address.sin_family = AF_INET;
	server_address.sin_port = _c_htons(port);

    let hostm = _c_gethostbyname(host)
    if not hostm? or not hostm.h_addr? {
        panic(`Error getting host by name, exiting`)
    }
    server_address.sin_addr.s_addr = *(hostm.h_addr as &i32)

	// Assigning IP address to struct
	if (_c_inet_pton(AF_INET, host, &server_address.sin_addr) < 0) {
        println(`inet_pton had an error: {strerror(errno)}`)
        exit(1)
  	}

    // Connecting to the socket
  	if (_c_connect(sock.fd, (&server_address) as &SockAddr, sizeof(SockAddrIn)) < 0) {
        println(`Error connecting to socket: {strerror(errno)}`)
        exit(1)
    }

    return sock
}

def Socket::read(&this, buf: &Buffer, max_size: i32): i32 {
    buf.resize_if_necessary(max_size)
    buf.size = _c_read(this.fd, buf.data, max_size)
    return buf.size
}

def Socket::read_exactly(&this, buf: &Buffer, size: i32): i32 {
    buf.resize_if_necessary(size)
    buf.size = 0
    while size > 0 {
        let n = _c_read(this.fd, buf.data + buf.size, size)
        if n < 0 {
            println(`Error reading from socket: {strerror(errno)}`)
            return -1
        }
        buf.size += n
        size -= n
    }
    return buf.size
}

def Socket::write(&this, buf: &Buffer): i32 => _c_write(this.fd, buf.data, buf.size)


/// Internal stuff

@compiler c_include "sys/socket.h"
@compiler c_include "netinet/in.h"
@compiler c_include "arpa/inet.h"
@compiler c_include "unistd.h"
@compiler c_include "errno.h"
@compiler c_include "sys/types.h"
@compiler c_include "time.h"
@compiler c_include "netdb.h"

struct SockAddr extern("struct sockaddr")
struct HostEnt extern("struct hostent") {
    h_addr: &u8 // this type is a lie
}
struct SinAddr extern("struct in_addr") {
    s_addr: i32
}
struct SockAddrIn extern("struct sockaddr_in") {
    sin_family: i32
    sin_port: i32
    sin_addr: SinAddr
}

def _c_htons(val: i32): i32 extern("htons")
def _c_htonl(val: i32): i32 extern("htonl")

let AF_INET: i32 extern
let SOCK_STREAM: i32 extern
let INADDR_ANY: i32 extern
let IPPROTO_UDP: i32 extern
let IPPROTO_TCP: i32 extern
let SOCK_DGRAM: i32 extern

def _c_socket(domain: i32, type: i32, protocol: i32): i32 extern("socket")
def _c_bind(sockfd: i32, addr: &SockAddr, addrlen: i32): i32 extern("bind")
def _c_listen(sockfd: i32, backlog: i32): i32 extern("listen")
def _c_accept(sockfd: i32, addr: &SockAddr, addrlen: &i32): i32 extern("accept")
def _c_inet_pton(af: i32, src: string, dst: untyped_ptr): i32 extern("inet_pton")
def _c_connect(sockfd: i32, addr: &SockAddr, addrlen: i32): i32 extern("connect")
def _c_gethostbyname(name: string): &HostEnt extern("gethostbyname")

def _c_read(fd: i32, buf: untyped_ptr, count: i32): i32 extern("read")
def _c_write(fd: i32, buf: untyped_ptr, count: i32): i32 extern("write")
def _c_close(fd: i32): i32 extern("close")